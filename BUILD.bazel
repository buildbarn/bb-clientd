load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@gazelle//:def.bzl", "gazelle")
load("@rules_pkg//:pkg.bzl", "pkg_deb", "pkg_tar")

# gazelle:go_naming_convention_external import
# gazelle:prefix github.com/buildbarn/bb-clientd
# gazelle:proto_import_prefix github.com/buildbarn/bb-clientd
# gazelle:resolve go github.com/bazelbuild/remote-apis/build/bazel/remote/execution/v2 @bazel_remote_apis//build/bazel/remote/execution/v2:remote_execution_go_proto
# gazelle:resolve go github.com/bazelbuild/remote-apis/build/bazel/semver @bazel_remote_apis//build/bazel/semver:semver_go_proto
# gazelle:resolve_regexp proto github.com/buildbarn/bb-remote-execution/(.*)/(.*)\.proto @com_github_buildbarn_bb_remote_execution//$1:${2}_proto
# gazelle:resolve_regexp proto github.com/buildbarn/bb-storage/(.*)/(.*)\.proto @com_github_buildbarn_bb_storage//$1:${2}_proto
# gazelle:resolve_regexp proto go github.com/buildbarn/bb-remote-execution/(.*)/(.*)\.proto @com_github_buildbarn_bb_remote_execution//$1:$2
# gazelle:resolve_regexp proto go github.com/buildbarn/bb-storage/(.*)/(.*)\.proto @com_github_buildbarn_bb_storage//$1:$2
# gazelle:resolve_regexp proto google/protobuf/(.*)\.proto @protobuf//:${1}_proto
gazelle(
    name = "gazelle",
)

pkg_tar(
    name = "bb_clientd_deb_data",
    extension = "tar.gz",
    files = {
        "//cmd/bb_clientd": "/usr/bin/bb_clientd",
        ":configs/bb_clientd.jsonnet": "/usr/lib/bb_clientd/bb_clientd.jsonnet",
        ":configs/linux/launch_bb_clientd_linux.sh": "/usr/lib/bb_clientd/launch_bb_clientd_linux.sh",
        ":configs/linux/logind.conf": "/etc/systemd/logind.conf.d/bb_clientd.conf",
        ":configs/linux/systemd.service": "/usr/lib/systemd/user/bb_clientd.service",
    },
)

# When built with --stamp, creates a non-deterministic output file for pushing images to a remote registry.
# With --nostamp, produces a deterministic output so dependents get cache hits.
expand_template(
    name = "deb_version_file",
    out = "_deb_version.txt",
    stamp_substitutions = {"_TAG_": "{{BUILD_SCM_TIMESTAMP}}-{{BUILD_SCM_REVISION}}"},
    substitutions = {"_TAG_": "0~unknown"},
    template = ["_TAG_"],
    visibility = ["//visibility:public"],
)

pkg_deb(
    name = "bb_clientd_deb",
    architecture = select({
        "@platforms//cpu:x86_64": "amd64",
        "//conditions:default": "unknown",
    }),
    data = ":bb_clientd_deb_data",
    depends = ["fuse"],
    description = "The Buildbarn client daemon",
    homepage = "https://github.com/buildbarn/bb-clientd",
    maintainer = "The Buildbarn team",
    package = "bb-clientd",
    package_file_name = "bb-clientd.deb",
    postinst = "configs/linux/postinst.sh",
    version_file = ":deb_version_file",
)
